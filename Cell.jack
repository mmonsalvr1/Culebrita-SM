//Representa una celda del tablero (segmento o comida): coordeenadas, tipo y rutinas de pintar/choque
class Cell {
  field int xCoord, yCoord;
  field int foodType;
  static int blockW, margin;

  // inicializa coordenadas x,y y tipo de comida por defecto 
  constructor Cell new(int x, int y) {
    let xCoord = x;
    let yCoord = y;
    let foodType = 0;
    return this;
  }

  // Genera una celda aleatoria que no coincide con la serpiente y asigna tipo (0 normal,1 buena,2 mala)
  function Cell newRandBlock(Culebrita snake, int l) {
    var int randX, randY, i, blockX, blockY, randType, r;
    var Cell randBlock, tmpBlock;
    var boolean matched;
    let randX = RNG.randRange(50);
    let randY = RNG.randRange(24);
    let i = 0;
    let matched = false;
    while (i < l) {
      let tmpBlock = snake[i];
      let blockX = tmpBlock.getCoord(true); 
      let blockY = tmpBlock.getCoord(false);
      if ((blockX = randX) & (blockY = randY)) {
        let matched = true;
      }
      let i = i + 1; 
    }
    while (matched) {
      let matched = false;
      let randX = RNG.randRange(50);
      let randY = RNG.randRange(24);
      let i = 0;
      while (i < l) {
        let tmpBlock = snake[i];
        let blockX = tmpBlock.getCoord(true); 
        let blockY = tmpBlock.getCoord(false);
        if ((blockX = randX) & (blockY = randY)) {
          let matched = true;
        }
        let i = i + 1; 
      }
    }
    let randBlock = Cell.new(randX, randY);
        let r = RNG.randRange(9);
        if (r < 2) {
          do randBlock.setFoodType(1);
        } else {
          if (r > 7) {
            do randBlock.setFoodType(2);
          } else {
            do randBlock.setFoodType(0);
          }
        }
        do Cell.drawRandBlock(randBlock);
        return randBlock;
      }

  method void setFoodType(int ft) {
    let foodType = ft;
    return;
  }

  method int getFoodType() {
    return foodType;
  }

  method int getCoord(boolean x) {
    var int retCoord;
    if (x) {
      let retCoord = xCoord;
    } else {
      let retCoord = yCoord;
    }
    return retCoord;
  }

  // Comprueba si la cabeza coincide con target. dir:1=der,2=izq,3=arr,4=aba
  function boolean matched(Cell head, Cell target, int dir) {
    var int headX, headY, targetX, targetY;
    var boolean matched;
    let matched = false;
    let headX = head.getCoord(true);
    let headY = head.getCoord(false);
    if (dir = 1){let headX = headX + 1;}
    if (dir = 2){let headX = headX - 1;}
    if (dir = 3){let headY = headY - 1;}
    if (dir = 4){let headY = headY + 1;}
    let targetX = target.getCoord(true);
    let targetY = target.getCoord(false);
    if ((headX = targetX) & (headY = targetY)) {
      let matched = true; 
    }
    return matched;
  } 

  // Crea y dibuja la nueva cabeza movida en `dir`
  function Cell moveHeadBlock(Cell headBlock, int dir) {
    var int newXCoord, newYCoord;
    var Cell newHeadBlock;
    let newXCoord = headBlock.getCoord(true);
    let newYCoord = headBlock.getCoord(false);
    if (dir = 1) {let newXCoord = newXCoord + 1;}
    if (dir = 2) {let newXCoord = newXCoord - 1;}
    if (dir = 3) {let newYCoord = newYCoord - 1;}
    if (dir = 4) {let newYCoord = newYCoord + 1;}
    let newHeadBlock = Cell.new(newXCoord, newYCoord);
    do Cell.drawFullBlock(newHeadBlock, true);
    return newHeadBlock;
  }
 
  // Comprueba si el movimiento en dir provoca p√©rdida (pared o cuerpo)
  function boolean seeIfLose(int l, Culebrita snake, int dir) {
    var boolean bumpWall, bumpBody, lose;
    var int headX, headY, i, newL, tmpX, tmpY;
    var Cell head, tmpBlock;
    let bumpWall = false;
    let bumpBody = false;
    let lose = false;
    let newL = l - 1;
    let head = snake[newL];
    let headX = head.getCoord(true);
    let headY = head.getCoord(false);
    if (dir = 1){let headX = headX + 1;}
    if (dir = 2){let headX = headX - 1;}
    if (dir = 3){let headY = headY - 1;}
    if (dir = 4){let headY = headY + 1;}
    if ((headX < 0) | (headX > 50) | (headY > 24) | (headY < 0)) {
      let bumpWall = true; 
    }
    let i = 1;
    while (i < newL) {
      let tmpBlock = snake[i];
      let tmpX = tmpBlock.getCoord(true);
      let tmpY = tmpBlock.getCoord(false);
      if ((headX = tmpX) & (headY = tmpY)) {
        let bumpBody = true;
      }
      let i = i + 1;
    }
    if ((bumpWall) | (bumpBody)) {
      let lose = true;
    }
    return lose;
  }
 
  // Libera la memoria asociada a una celda
  function void dispose(Cell block) {
    do Memory.deAlloc(block);
    return;
  }

  function void setBlockInfo(int bw, int m) {
    let blockW = bw;
    let margin = m;
    return;
  }

  // Dibuja la celda completa
  function void drawFullBlock(Cell block, boolean color) {
    var int x1, y1, x2, y2;
    var int xIndx, yIndx;
    let xIndx = block.getCoord(true);
    let yIndx = block.getCoord(false);
    let x1 = xIndx * blockW + margin;
    let y1 = yIndx * blockW + margin;
    let x2 = xIndx * blockW + blockW - margin;
    let y2 = yIndx * blockW + blockW - margin;
    if (color) {
      do Screen.setColor(true);
    } else {
      do Screen.setColor(false);
    }
    do Screen.drawRectangle(x1, y1, x2, y2);
    return;
  }
  
  // Dibuja la celda de comida
  function void drawRandBlock(Cell randBlock) {
    var int innerX1, innerY1, innerX2, innerY2;
    var int xIndx, yIndx;
    do Cell.drawFullBlock(randBlock, true);
    let xIndx = randBlock.getCoord(true);
    let yIndx = randBlock.getCoord(false);
    let innerX1 = xIndx * blockW + margin + 1;
    let innerY1 = yIndx * blockW + margin + 1;
    let innerX2 = xIndx * blockW + blockW - margin - 1; 
    let innerY2 = yIndx * blockW + blockW - margin - 1; 
    do Screen.setColor(false);
    do Screen.drawRectangle(innerX1, innerY1, innerX2, innerY2);
    return;
  }

  //devuelve las coordenadas de la celda
  function void printBlockCoord(Cell block) {
    var String uParam, comma, lParam;
    var int xIndx, yIndx;
    let xIndx = block.getCoord(true);
    let yIndx = block.getCoord(false);
    let uParam = "(";
    let comma = ",";
    let lParam = ")";
    do Output.printString(uParam);
    do Output.printInt(xIndx);
    do Output.printString(comma);
    do Output.printInt(yIndx);
    do Output.printString(lParam);
    do Output.println();
    do uParam.dispose();
    do comma.dispose();
    do lParam.dispose();
    return;
  }
}
